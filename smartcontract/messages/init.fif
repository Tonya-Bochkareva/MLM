"TonUtil.fif" include
"lib/cli.fif" include

"output.pk" =: DEFAULT_OUTPUT_PK
"addr.bin"  =: DEFAULT_OUTPUT_ADDR
"output"    =: DEFAULT_OUTPUT_DIR

{
  ."[OPTIONS] <filename> <target>" cr
  ."Creates MLM smarcontract" cr cr

  ."  <filename>              smarcontract source code in fift" cr
  ."  <target>                target smarcontract for transfering grams" cr cr

  ."OPTIONS:" cr
  ."  -k,--key output.pk      private key file" cr
  ."                            default=" DEFAULT_OUTPUT_PK type cr
  ."  -a,--address addr.bin   file for storing scm address in binary format" cr
  ."                            default=" DEFAULT_OUTPUT_ADDR type cr
  ."  -d,--dir dirname        output folder" cr
  ."                            default=" DEFAULT_OUTPUT_DIR type
} : usage()

{
  "-d" "--dir" 1 { =: OUTPUT_DIR } option-long()
  "-k" "--key" 1 { =: OUTPUT_PK } option-long()
  "-a" "--address" 1 { =: OUTPUT_ADDRESS } option-long()
  1 { include =: SOURCE_CODE } nth-argument()
  2 { =: TARGET } nth-argument()
} scan-args()

2 < {
  show-usage()
} if

def? OUTPUT_DIR { @' OUTPUT_DIR } { DEFAULT_OUTPUT_DIR } cond constant OUTPUT_DIR
def? OUTPUT_PK { @' OUTPUT_PK } { DEFAULT_OUTPUT_PK } cond constant OUTPUT_PK
def? OUTPUT_ADDR { @' OUTPUT_ADDR } { DEFAULT_OUTPUT_ADDR } cond constant OUTPUT_ADDR

OUTPUT_DIR "/" $+ OUTPUT_PK $+ constant OUTPUT_PK
OUTPUT_DIR "/" $+ OUTPUT_ADDR $+ constant OUTPUT_ADDR

<b
  0 32 u, // seqno
  OUTPUT_PK load-generate-keypair constant wallet_pk dup constant public_key
  B, // public key
  b{001} s, TARGET $>smca 2drop addr,
  <b b{0} s, b> ref,
  <b null dict, b> ref,
b> swap

<b
  // split_depth:(Maybe (## 5)) special:(Maybe TickTock)
  b{00} s,                // nothing$0
  // code:(Maybe ^Cell)
  b{1} s,                 // just$1
    swap ref,             // ^Cell
  b{1} s,                 // data:(Maybe ^Cell)
    swap ref,
  b{0} s,                 // library:(HashmapE 256 SimpleLib)
b>

dup hash ${WC} swap 2constant addr
addr OUTPUT_ADDR save-address

<b 0 32 u, 0 4 u, b>
dup hash wallet_pk ed25519_sign_uint
rot

<b
  // info:CommonMsgInfo
  b{10} s,               // ext_in_msg_info$10
    // src:MsgAddressExt
    b{00} s,             // addr_none$00
    // dest:MsgAddressInt
    b{10} s,             // addr_std$10
      b{0} s,            // anycast:(Maybe Anycast)
      addr addr,         // workchain_id:int8 address:bits256
    // import_fee:Grams
    0 Gram,              // (VarUInteger 16) // b{0000} s,
  // init:(Maybe (Either StateInit ^StateInit))
  b{1} s,                // just$1
    b{1} s,              // right$1
      swap ref,          // ^StateInit
  // body:(Either X ^X)
  b{0} s,                // left$0 0
    swap B, swap <s s,
b>
dup <s ."INIT Message:" cr csr.
cr
."Smart contract addr: " addr .addr cr
."Smart contract non-bounceable: " addr 7 .Addr cr
."Smart contract bounceable: " addr 6 .Addr cr
